buildscript {
    ext {
        kotlin_version = '1.5.20'
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.20"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.plugins.findPlugin('android') ?: project.plugins.findPlugin('android-library')) {
            android {
                compileSdkVersion Config.compile_sdk
                buildToolsVersion Config.build_tools

                defaultConfig {
                    minSdkVersion Config.min_sdk
                    targetSdkVersion Config.target_sdk
                    versionCode Releases.version_code
                    versionName Releases.version_name
                    consumerProguardFiles Releases.consumer_proguard_files
                    testInstrumentationRunner Tests.runner
                }
                buildTypes {
                    release {
                        minifyEnabled false
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }
                compileOptions {
                    sourceCompatibility Config.java_version
                    targetCompatibility Config.java_version
                }

                kotlinOptions {
                    jvmTarget = KotlinOptions.jvm_target
                }
            }
            dependencies {
                //Kotlin
                implementation Kotlin.stdlib
                implementation Kotlin.core

                //Test
                testImplementation TestImpl.junit
                androidTestImplementation TestImpl.androidx_junit
                androidTestImplementation TestImpl.espresso
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}